/*
 * Using built-in Gradle support for Jmods (JPMS) instead of  javafx-gradle-plugin (org.openjfx.javafxplugin).
 * See https://stackoverflow.com/questions/63997525/task-run-causes-org-joor-reflectexception-java-lang-nosuchfieldexception-ja
 */


plugins {
	id 'application'
}

def currentOS = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem;
def platform
if (currentOS.isWindows()) {
	 platform = 'win'
} else if (currentOS.isLinux()) {
	 platform = 'linux'
} else if (currentOS.isMacOsX()) {
	 platform = 'mac'
}

java {
	modularity.inferModulePath = true
}



sourceCompatibility=1.11
targetCompatibility=1.11


repositories {
	mavenCentral();
}

dependencies {
	implementation "org.openjfx:javafx-base:17-ea+3:${platform}"
	implementation "org.openjfx:javafx-controls:17-ea+3:${platform}"
	implementation "org.openjfx:javafx-graphics:17-ea+3:${platform}"
	implementation "org.openjfx:javafx-fxml:17-ea+3:${platform}"
	implementation group: 'org.json', name: 'json', version: '20210307'
	//log4j with slf4j binding required by hikariCP
	implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'
	implementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.14.1'
	// db access
	implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'
	implementation group: 'com.oracle.database.jdbc', name: 'ojdbc8-production', version: '19.8.0.0'
	implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'
	// using JPA without application server
	implementation group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: '3.0.0'
	implementation group: 'org.hibernate.orm', name: 'hibernate-hikaricp', version: '6.0.0.Alpha7'
	implementation group: 'org.hibernate.orm', name: 'hibernate-core', version: '6.0.0.Alpha7'
	// unit test framework
	testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.8.0-M1'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.8.0-M1'
}

sourceSets {
	test {
		resources {
			srcDir 'src/test/resources'
			// TODO unnecessary??
			include 'src/main/resources/config.json'
			include 'src/main/resources/META-INF/persistence.xml'
		}
	}
}

test {
	// Activate JUnit native support. Otherwise some JUnit 5(Jupiter) annotations may not be recognized.
	useJUnitPlatform()
	// Explicitly list every executed test in console.
	beforeTest { descriptor ->
		 logger.lifecycle("Running test: ${descriptor}")
	}
}


/*
 * TODO workaround for ensuring  'src/main/resources/META-INF/persistence.xml'
 * is available at runtime under 'build/classes/main/META-INF/persistence.xml'
 */
sourceSets.main.output.resourcesDir = sourceSets.main.java.outputDir
jar {
	duplicatesStrategy = 'exclude'
}
// Ensure 'src/main/resources/META-INF/persistence.xml' is visible at compile time.
compileJava.dependsOn(processResources)


application {
	/*
	mainModule = 'imperfectsilentart.martinfowler.uiArchs.formsandcontrols'
	mainClass = "imperfectsilentart.martinfowler.uiArchs.formsandcontrols.IceCreamAssessmentForm"
	*/
	mainModule = 'imperfectsilentart.martinfowler.uiArchs.mvc_standalone'
	mainClass = 'imperfectsilentart.martinfowler.uiArchs.mvc_standalone.MvcStandaloneLauncher'
	
}


